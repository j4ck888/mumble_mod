# Copyright 2022 The Mumble Developers. All rights reserved.
# Use of this source code is governed by a BSD-style license
# that can be found in the LICENSE file at the root of the
# Mumble source tree or at <https://www.mumble.info/LICENSE>.

option(bundled-soci "Build the included version of SOCI instead of looking for one on the system" ON)

add_library(mumble_database STATIC
	"Backend.cpp"
	"Column.cpp"
	"Constraint.cpp"
	"ConversionUtils.cpp"
	"DataType.cpp"
	"ForeignKey.cpp"
	"Database.cpp"
	"Index.cpp"
	"MetaTable.cpp"
	"MySQLConnectionParameter.cpp"
	"PostgreSQLConnectionParameter.cpp"
	"PrimaryKey.cpp"
	"SQLiteConnectionParameter.cpp"
	"Table.cpp"
	"TransactionHolder.cpp"
	"Trigger.cpp"
	"Utils.cpp"
	"Version.cpp"
)

target_include_directories(mumble_database PUBLIC "${CMAKE_SOURCE_DIR}/src")

if (bundled-soci)
	# Include SOCI, but hard-code a few options
	set(SOCI_BINARY_DIR ${CMAKE_CURRENT_BINARY_DIR}/soci)

	set(SOCI_SHARED ON CACHE BOOL "" FORCE)
	set(SOCI_STATIC ON CACHE BOOL "" FORCE)
	set(SOCI_TESTS OFF CACHE BOOL "" FORCE)
	set(SOCI_LTO ${lto} CACHE BOOL "" FORCE)
	set(SOCI_DB2 OFF CACHE BOOL "" FORCE)
	set(SOCI_FIREBIRD OFF CACHE BOOL "" FORCE)
	set(SOCI_ODBC OFF CACHE BOOL "" FORCE)
	set(SOCI_ORACLE OFF CACHE BOOL "" FORCE)
	set(SOCI_EMPTY OFF CACHE BOOL "" FORCE)
	set(SOCI_MYSQL ON CACHE BOOL "" FORCE)
	set(SOCI_POSTGRESQL ON CACHE BOOL "" FORCE)
	set(SOCI_SQLITE3 ON CACHE BOOL "" FORCE)

	add_subdirectory("${3RDPARTY_DIR}/soci" "${SOCI_BINARY_DIR}" EXCLUDE_FROM_ALL)

	disable_warnings_for_all_targets_in("${3RDPARTY_DIR}/soci")

	# SOCI's built include/ directory has to be added to the project explicitly.
	# This is needed so that SOCI can find the file soci/soci-config.h that gets generated by CMake.
	target_include_directories(mumble_database
		PUBLIC
			${SOCI_BINARY_DIR}/include
	)

	set(soci_components
		"soci_mysql_static"
		"soci_postgresql_static"
		"soci_sqlite3_static"
		"soci_core_static"
	)
else()
	# Only Soci 4.1 introduces the general BLOB operations that we require
	find_pkg("Soci" 4.1.0 COMPONENTS mysql postgresql sqlite3 core REQUIRED)

	set(soci_static "")
	set(soci_components
		"${Soci_mysql_PLUGIN}"
		"${Soci_postgresql_PLUGIN}"
		"${Soci_sqlite3_PLUGIN}"
		"${Soci_LIBRARY}"
	)
endif()

target_link_libraries(mumble_database PUBLIC ${soci_components})

# Note that it is SOCI that requires the public Boost dependency, but unfortunately seems to fail to properly
# declare so and thus linking against SOCI does not reveal this Boost dependency to upstream dependends.
find_pkg("Boost" REQUIRED)
target_link_libraries(mumble_database PUBLIC Boost::boost)


if (NOT TARGET nlohmann_json)
	if(bundled-json)
		set(JSON_BuildTests OFF CACHE INTERNAL "")
		set(JSON_ImplicitConversions OFF CACHE INTERNAL "")
		add_subdirectory("${3RDPARTY_DIR}/nlohmann_json/" "${CMAKE_CURRENT_BINARY_DIR}/nlohmann_json/" EXCLUDE_FROM_ALL)
	else()
		find_pkg("nlohmann_json" REQUIRED)
	endif()
endif()

target_link_libraries(mumble_database PUBLIC nlohmann_json)
